apply plugin: 'com.android.application'
android {
    compileSdkVersion 22
    buildToolsVersion "23.0.0 rc3"

    defaultConfig {
        applicationId 'pl.d30.binClock'
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 21
        versionName "0.6.1"

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            storeFile file(project.STORE_FILE)
            storePassword project.STORE_PASSWORD as String
            keyAlias project.KEY_ALIAS as String
            keyPassword project.KEY_PASSWORD as String
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release as Object
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true

            android.applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    if (output.name == "release") {
                        def file = output.outputFile
                        String parent = file.parent

                        if (project.hasProperty('OUTPUT_DIR') && new File(project.OUTPUT_DIR as String).exists())
                            parent = project.OUTPUT_DIR

                        output.outputFile = new File(
                            parent,
                            file.name.replace(
                                ".apk",
                                "-" + (defaultConfig.versionName as String) + "-build" + (defaultConfig.versionCode as String) + ".apk"
                            ) as String
                        )
                    }
                }
            }
        }
    }
}

dependencies {
    compile 'com.android.support:support-annotations:22.2.1'
    compile 'com.anjlab.android.iab.v3:library:1.0.26@aar'
    compile 'com.github.chester1000:colorpicker:3f9f95be81'
}