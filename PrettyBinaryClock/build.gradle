apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    defaultConfig {
        applicationId 'pl.d30.binClock'
        minSdkVersion 15
        targetSdkVersion 21
        versionCode 21
        versionName "0.6.1"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    signingConfigs {
        release {
            storeFile     file(STORE_FILE)
            storePassword STORE_PASSWORD
            keyAlias      KEY_ALIAS
            keyPassword   KEY_PASSWORD
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            zipAlignEnabled true
            // shrinkResources true

            android.applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    if (output.name == "release") {
                        def file = output.outputFile
                        String parent = file.parent

                        if (project.hasProperty('OUTPUT_DIR') && new File(OUTPUT_DIR as String).exists())
                            parent = OUTPUT_DIR

                        output.outputFile = new File(
                            parent,
                            file.name.replace(
                                ".apk",
                                "-" + (defaultConfig.versionName as String) + "-build" + (defaultConfig.versionCode as String) + ".apk"
                            ) as String
                        )
                    }
                }
            }
        }
    }
}